#!/usr/bin/env -S escript -c
-module(broadcast).
-export([init/0]).
-define(PROMPT, "").
-define(FORMAT, "~s~n").

-record(state, {
	node_id=null,
	store=[],
	topology=#{},
	callbacks=#{}}).

main([]) -> 
  io:setopts(standard_io, [{binary, true}]),
	spawn(?MODULE, init, []),
  loop().

loop() ->
  case io:get_line(?PROMPT) of
    Line when is_binary(Line) -> 
			server ! {line, Line},
      loop();
    eof -> stop()
  end.

stop() ->
	server ! {eof, self()},
	receive ok -> erlang:halt(0) end.

init() ->
	process_flag(trap_exit, true),
	register(server, self()),
	server(#state{}).

server(State) ->
	receive
		{line, Line} ->
      Msg = json:decode(Line),
			{ok, NewState} = handle(Msg, State),
			server(NewState);
		{eof, From} ->
			self() ! {stop, From},
			server(State);
		{stop, From} ->
			From ! ok;
		Msg ->
			{ok, NewState} = handle_info(Msg, State),
			server(NewState)
	end.

handle(Msg, State) ->
  #{<<"src">>  := Src,
    <<"dest">> := Dest,
    <<"body">> := Body} = Msg,
  #{<<"type">> := Tag} = Body,
  handle(Tag, {Src, Dest, Body}, State).

handle(<<"init">> = Tag, {Src, Dest, Body}, _State) ->
  #{<<"type">>     := Tag,
    <<"msg_id">>   := MsgId,
    <<"node_id">>  := NodeId,
    <<"node_ids">> := _NodeIds} = Body,

  reply(Src, Dest, #{
    <<"type">> => <<"init_ok">>,
    <<"in_reply_to">> => MsgId
  }, #state{node_id=NodeId});


handle(<<"broadcast">> = Tag, {Src, Dest, Body}, State) ->
  #{<<"type">>    := Tag,
    <<"msg_id">>  := MsgId,
    <<"message">> := Message} = Body,

  reply(Src, Dest, #{
    <<"type">> => <<"broadcast_ok">>,
    <<"msg_id">> => erlang:unique_integer([monotonic, positive]), 
    <<"in_reply_to">> => MsgId
  }, State),

	gossip(Src, Message, State),

  Store = [Message|State#state.store],
  NewState = State#state{store=Store},
	{ok, NewState};


handle(<<"read">> = Tag, {Src, Dest, Body}, State) ->
  #{<<"type">> := Tag, <<"msg_id">> := MsgId} = Body,

  reply(Src, Dest, #{
    <<"type">> => <<"read_ok">>,
    <<"msg_id">> => erlang:unique_integer([monotonic, positive]), 
    <<"in_reply_to">> => MsgId,
    <<"messages">> => State#state.store
  }, State);

handle(<<"topology">> = Tag, {Src, Dest, Body}, State) ->
  #{<<"type">>    := Tag,
    <<"msg_id">>  := MsgId,
    <<"topology">> := Topology} = Body,

  NewState = State#state{topology=Topology},

  reply(Src, Dest, #{
    <<"type">> => <<"topology_ok">>,
    <<"msg_id">> => erlang:unique_integer([monotonic, positive]), 
    <<"in_reply_to">> => MsgId
  }, NewState);

handle(<<"broadcast_ok">>, _Msg, State) -> {ok, State};
handle(_Tag, _Msg, State) -> {ok, State}.

handle_info({reply, Reply}, State) ->
	io:format(?FORMAT, [json:encode(Reply)]),
	{ok, State};

handle_info({rpc, Msg}, State) ->
	io:format(?FORMAT, [json:encode(Msg)]),
	{ok, State}.


gossip(Src, Message, State) ->
  NodeId = State#state.node_id,
  Topology = State#state.topology,
  maybe 
		#{NodeId := Neighbours} ?= Topology,
		false ?= lists:any(fun(X) -> X == Message end, State#state.store),
		lists:foreach(fun
			(N) when N =:= Src -> ok;
			(N) -> broadcast(N, Message, State)
		end, Neighbours)
  end.

reply(Dest, Src, Body, State) when State#state.node_id =:= Src ->
  reply(Dest, Body, State).

reply(Dest, Body, State) -> 
  Reply = #{
    <<"dest">> => Dest, 
    <<"src">>  => State#state.node_id,
    <<"body">> => Body
  },
	self() ! {reply, Reply},
	{ok, State}.

broadcast(Dest, Message, State) ->
  Body = #{
    <<"type">>    => <<"broadcast">>,
    <<"msg_id">>  => erlang:unique_integer([monotonic, positive]), 
    <<"message">> => Message},
  Msg = #{
    <<"dest">> => Dest, 
    <<"src">>  => State#state.node_id,
    <<"body">> => Body
  },
	%% TODO: {ok, _TRef} = timer:send_interval(1000, {rpc, Msg}).
	self() ! {rpc, Msg}.




